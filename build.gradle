//managed plugins versions
plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
    id 'nu.studer.jooq' version '4.1' apply false
    id 'org.springframework.boot' version '2.2.6.RELEASE' apply false
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id "com.moowork.node" version "1.3.1" apply false
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    tasks.withType(JavaCompile) {
        options.with {
            encoding = 'UTF-8'
        }
    }
    sourceCompatibility = 11
    targetCompatibility = 11
    group = 'ch.fhnw.speech_collection_app'
    version = "1.0.0-SNAPSHOT"

    //managed dependency versions
    dependencyManagement {
        dependencies {
            dependency 'org.apache.tika:tika-parsers:1.24'
            dependency 'org.flywaydb:flyway-core:6.3.3'
            dependencySet(group: 'org.jooq', version: '3.13.1') {
                entry 'jooq'
                entry 'jooq-codegen'
            }
            dependency 'org.mariadb.jdbc:mariadb-java-client:2.6.0'
        }
    }
}
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
