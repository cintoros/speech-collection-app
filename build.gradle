//managed plugins versions
plugins {
    id "io.spring.dependency-management" version "1.0.9.RELEASE" apply false
    id 'nu.studer.jooq' version '4.1' apply false
    id 'org.springframework.boot' version '2.2.4.RELEASE' apply false
    id 'se.patrikerdes.use-latest-versions' version '0.2.13'
    id 'com.github.ben-manes.versions' version '0.27.0'
    id "com.moowork.node" version "1.3.1" apply false
}

allprojects {
    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'

    tasks.withType(JavaCompile) {
        options.with {
            encoding = 'UTF-8'
        }
    }
    sourceCompatibility = 11
    targetCompatibility = 11
    group = 'ch.i4ds.new_project'
    version = "1.0.0-SNAPSHOT"

    //managed dependency versions
    dependencyManagement {
        dependencies {
            dependency 'ch.qos.logback:logback-classic:1.2.3'
            dependency 'com.google.guava:guava:28.2-jre'
            dependency 'commons-io:commons-io:2.6'
            dependency 'org.apache.commons:commons-csv:1.8'
            dependency 'org.apache.commons:commons-math3:3.6.1'
            dependency 'org.apache.tika:tika-parsers:1.23'
            dependency 'org.flywaydb:flyway-core:6.2.2'
            dependencySet(group: 'org.jooq', version: '3.11.10') {
                entry 'jooq'
                entry 'jooq-codegen'
            }
            dependency 'org.mariadb.jdbc:mariadb-java-client:2.5.4'
            dependencySet(group: 'org.slf4j', version: '1.7.30') {
                entry 'slf4j-api'
                entry 'log4j-over-slf4j'
                entry 'jcl-over-slf4j'
                entry 'jul-to-slf4j'
            }
        }
    }
}
def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA', 'JRE'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}
dependencyUpdates {
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
