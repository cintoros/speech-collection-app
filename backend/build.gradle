import org.springframework.boot.gradle.tasks.run.BootRun

apply plugin: 'nu.studer.jooq'
apply plugin: 'org.springframework.boot'

description 'module containing the whole spring framework logic'

dependencies {
    implementation 'org.apache.tika:tika-parsers'
    implementation 'org.flywaydb:flyway-core'
    implementation 'org.mariadb.jdbc:mariadb-java-client'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-jooq'
    implementation 'org.springframework.boot:spring-boot-starter-security'
    implementation 'org.springframework.boot:spring-boot-starter-mail'
    implementation 'org.springframework.boot:spring-boot-starter-freemarker'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'

    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'

    jooqRuntime 'org.mariadb.jdbc:mariadb-java-client'
}
task devBootRun(type: BootRun) {
    main = 'ch.fhnw.speech_collection_app.DevTestApplication'
    classpath += test.classpath
}

jooq {
    generateSchemaSourceOnCompilation = false
    sample(sourceSets.main) {
        jdbc {
            driver = 'org.mariadb.jdbc.Driver'
            url = 'jdbc:mysql://localhost/speech_collection_app'
            user = 'speech_collection_app'
            password = 'speech_collection_app'
        }
        generator {
            name = 'org.jooq.codegen.DefaultGenerator'
            database {
                inputSchema = 'speech_collection_app'
                forcedTypes {
                    forcedType {
                        name = 'BOOLEAN'
                        expression = '.*'
                        types = '(?i:(TINY|SMALL|MEDIUM|BIG)?INT(UNSIGNED)?\\(1\\))'
                    }
                }
            }
            generate {
                comments = false
                pojos = true
                immutablePojos = false
                javadoc = false
                generatedAnnotation = false
                validationAnnotations = true
                springAnnotations = true
                records = true
                //disable java localdate etc.
                javaTimeTypes = false
            }
            target {
                packageName = 'ch.fhnw.speech_collection_app.jooq'
                directory = 'src/main/java'
            }
        }
    }
}
task buildProd {
    dependsOn ':frontend:npmBuild'
    finalizedBy 'build'
}

//angular resources
processResources {
    from('../frontend/dist/speech_collection_app') { into 'public' }
}
